;  expensive water pixel shader used by A3DTerrainWater. 

; t0, bump map, not used
; t1, normal map for water surface
; t2, reflect map
; t3, reflect vector in world space, packed to [0.0, 1.0]
; t4, refract map

ps.1.4

def c1, 0.2, 0.12, 0.0, 0.0
def c2, 0.0, 0.0, 0.0, 1.0
def c3, 0.8, 0.8, 0.8, 1.0
def c4, 0.0, 0.5, 0.5, 1.0

texld	r1, t1
texcrd	r2.xy, t2_dw.xyw
texcrd	r3.xy, t3_dw.xyw
texcrd	r4.xy, t2_dw.xyw

mov r0.r, r1.r			// we use r=x, g=y, b=z in normal map
mov r0.g, r1.b

mad r2.rg, r0_bx2, c1.x, r2	// perturb coords by the normal
mad r4.rg, r0_bx2, c1.y, r4
mov r2.b, c3.a
mov r3.b, c3.a
mov r4.b, c3.a

phase

texld r2, r2
texld r4, r4

// calculate fresnel effects : R0 + (1-R0) (1-cos(q))^5
dp3_sat r1.rgba, r3_bx2, r1_bx2
mul r0.a, 1-r1.a, 1-r1.a			// squared
mul r0.a, r0.a, r0.a				// quartic
mul r0.rgb, r2, c3				// reflect color
+mul_sat r0.a, r0.a, 1-r1.a			// quintic

mul_x4 r1.rgb, v1, r0.a				// specular color
+lrp r0.a, r0.a, c2.a, c1.a

mul r4, r4, c4
mad r0.rgb, r0, r0.a, r4

add r0.rgb, r1, r0				// apply specular
+mov r0.a, c3.a
